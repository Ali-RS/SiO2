buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // This sucks down a lot of stuff that 'normal' users wouldn't 
        // have and won't need anyway.
        if( project.hasProperty('releaseUser') ) {
            classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        }
    }
}

apply plugin: 'java'
apply plugin: 'maven'

version='1.1.0'
group='com.simsilica'

ext.jmeVersion = "3.1.0-alpha4" 

// Version meta-data
ext {
    releaseDescription = "SiO2 ${project.version}"    
    releaseVcsTag = "${project.name}-v${project.version}"    
}

// Project-wide meta-data
ext {
    bintrayLabels = ['jMonkeyEngine', 'gamedev', 'util']
    websiteUrl = 'https://github.com/Simsilica/SiO2'
    vcsUrl = 'https://github.com/Simsilica/SiO2.git'
    githubRepo = 'Simsilica/SiO2'
    issueTrackerUrl = 'https://github.com/Simsilica/SiO2/issues'
}

repositories {
    mavenLocal()
    jcenter()
    
    // Temporary until JME jars are in jcenter()   
    maven { url "http://dl.bintray.com/jmonkeyengine/org.jmonkeyengine" }        
}

// Make sure the build file declares what it actually imports
configurations {
    compile.transitive = false
}


// In this section you declare the dependencies for your production and test code
dependencies {
    compile 'com.google.guava:guava:19.0'
    
    compile "org.jmonkeyengine:jme3-core:$jmeVersion" 
    compile "org.jmonkeyengine:jme3-networking:$jmeVersion" 
    
    compile "com.simsilica:lemur:1.6.1"

    compile "com.simsilica:zay-es:1.2.1"
    compile "com.simsilica:zay-es-net:1.2.1"
 
    compile 'org.codehaus.groovy:groovy-all:2.4.5'
    
    compile 'org.slf4j:slf4j-api:1.7.13'
    compile 'org.apache.logging.log4j:log4j-api:2.5'
    compile 'org.apache.logging.log4j:log4j-core:2.5'    
}


// Configuration to produce maven-repo style -sources and -javadoc jars
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    exclude '**/.backups' 
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
 
artifacts {
    archives sourcesJar
    archives javadocJar
}


// Put this at the end or it won't pick up the project.version and stuff
if( project.hasProperty('releaseUser') ) {
    apply plugin: 'com.jfrog.bintray'
    apply from: 'https://raw.githubusercontent.com/Simsilica/gradle-plugins/master/simtools-release.gradle'
}


